# Full PEG+Actions parser
# =======================
top:
    | *rules=( rule | ~ end_line )+ !. { Grammar(*rules) }

# repeatedly needed clauses
end_line:
    | spaces [ "#" (!\n .)* ] ( \n | !. )
spaces:
    | " "*
identifier:
    | ( ( "a" - "z" ) | ( "A" - "Z" ) | "_" )+
literal:
    | '"' body=(!'"' .)+ '"' | "'" body=(!"'" .)+ "'" {body}

# rule and action parsing
rule:
    | name=identifier ":" end_line ~ *cases=(" " spaces "|" ~ spaces rule_case end_line)+ { Rule(name, Choice(*cases)) }
    | name=identifier ":" ~ expr=rule_case { Rule(name, expr) }
rule_case:
    | expr=expr spaces action=action { Transform(expr, action) }
    | expr
action:
    | "{" body=action_body ~ "}" { body }
action_body:
    | (!"{" !"}" .)+
    | "{" action_body ~ "}"

# clause producing expressions
# the top expression, recursively matching all other expressions
expr:
    | choice

choice:
    | head=choice spaces *tail=("|" ~ spaces sequence) { Choice(head, *tail) }
    | sequence

sequence:
    | *head=(capture spaces)+ "~" ~ spaces tail=sequence { Sequence(*head, Entail(tail)) }
    | head=capture *tail=(spaces capture)+ { Sequence(head, *tail) }
    | "~" ~ spaces tail=sequence { Entail(tail) }
    | capture

capture:
    | "*" ~ name=identifier "=" expr=repeat { Capture(expr, name, variadic=True) }
    | name=identifier "=" ~ expr=repeat { Capture(expr, name, variadic=False) }
    | repeat

repeat:
    | expr=prefix "+" { Repeat(expr) }
    | expr=prefix "*" { Choice(Repeat(expr), Empty()) }
    | prefix

prefix:
    | "!" spaces expr=prefix { Not(expr) }
    | "&" spaces expr=prefix { And(expr) }
    | "(" spaces ~ expr spaces ")"
    | "[" spaces ~ expr=expr spaces "]" { Choice(expr, Empty()) }
    | atom

atom:
    | "''" | '""' { Empty() }
    | "." { Any(1) }
    | "\n" { Value("\n") }
    | lower=literal spaces "-" ~ spaces upper=literal { Range(lower, upper) }
    | literal=literal { Value(literal) }
    | name=identifier { Reference(name) }
